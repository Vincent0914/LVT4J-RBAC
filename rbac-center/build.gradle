buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:2.1.3.RELEASE')
        classpath('gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.2.2')
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

jar {
    enabled=true
}

gitProperties {
    extProperty = 'gitProps'
    dateFormat = 'yyyy-MM-dd HH:mm:ss'
}

springBoot {
    buildInfo{
        dependsOn(generateGitProperties);
        def tmpVer = version
        doFirst{
            tmpVer = version
            version = project.ext.gitProps['git.commit.id.abbrev']+' '+project.ext.gitProps['git.commit.time']+' '+project.ext.gitProps['git.commit.message.short']
        }
        properties{
            additional = [
                time : new java.text.SimpleDateFormat('yyyy-MM-dd HH:mm:ss').format(new Date())
            ]
        }
        doLast{
            version = tmpVer
        }
    }
}

dependencies {
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    api project(':rbac-client')
    
    api 'com.google.guava:guava:16.0.1'
    api 'org.apache.commons:commons-lang3:3.4'
    api 'commons-configuration:commons-configuration:1.10'
    api 'commons-io:commons-io:2.5'
    api 'org.apache.commons:commons-collections4:4.3'
    
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-actuator'
    api 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.0.1'
    api 'com.baomidou:mybatis-plus-boot-starter:3.1.0'
    api 'de.codecentric:spring-boot-admin-starter-client:2.1.6'
    
    api 'org.apache.velocity:velocity:1.7'
    api 'velocity-tools:velocity-tools-view:1.4'
    
    api 'com.hazelcast:hazelcast:4.1.1'
    
    api 'org.xerial:sqlite-jdbc:3.16.1'
    api 'com.h2database:h2:1.4.200'
    api 'mysql:mysql-connector-java:6.0.6'
}